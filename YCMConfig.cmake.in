# SPDX-FileCopyrightText: 2012-2021 Istituto Italiano di Tecnologia (IIT)
# SPDX-License-Identifier: BSD-3-Clause

# A macro to print a warning when using deprecated variables, called by
# variable_watch
macro(_YCM_DEPRECATED_VARIABLE_WARNING _variable)
  message(DEPRECATION "The ${_variable} variable is deprecated")
endmacro()

set(YCM_VERSION @YCM_VERSION@)
set(YCM_VERSION_MAJOR @YCM_VERSION_MAJOR@)
set(YCM_VERSION_MINOR @YCM_VERSION_MINOR@)
set(YCM_VERSION_PATCH @YCM_VERSION_PATCH@)

@PACKAGE_INIT@

set_and_check(YCM_MODULE_DIR "@PACKAGE_YCM_MODULE_DIR@")

unset(YCM_MODULE_PATH)
list(APPEND YCM_MODULE_PATH "${YCM_MODULE_DIR}/modules")
list(APPEND YCM_MODULE_PATH "${YCM_MODULE_DIR}/find-modules")
list(APPEND YCM_MODULE_PATH "${YCM_MODULE_DIR}/build-modules")
list(APPEND YCM_MODULE_PATH "${YCM_MODULE_DIR}/style-modules")

# "Newer modules should be found earlier in case of files with the same name,
# therefore the order is
#  - proposed
#  - next
#  - old releases (newer releases first)
#  - 3rd party
#  - deprecated

# Use modules from CMake (default ON)
if(NOT DEFINED YCM_USE_CMAKE OR YCM_USE_CMAKE)

  # Use modules including unmerged CMake patches (default OFF)
  if(YCM_USE_CMAKE_PROPOSED)
    list(APPEND YCM_MODULE_PATH "${YCM_MODULE_DIR}/cmake-proposed/Modules")
  endif()

  # Use modules from unreleased CMake (default ON)
  if(CMAKE_VERSION VERSION_LESS 3.22)
    set(YCM_USE_CMAKE_NEXT_DEFAULT_VALUE ON)
  else()
    set(YCM_USE_CMAKE_NEXT_DEFAULT_VALUE OFF)
  endif()
  option(YCM_USE_CMAKE_NEXT "Use legacy YCM fork of ExternalProject module instead of upstream CMake ExternalProject" ${YCM_USE_CMAKE_NEXT_DEFAULT_VALUE})
  if(NOT YCM_USE_CMAKE_NEXT AND CMAKE_VERSION VERSION_LESS 3.22)
    message(FATAL_ERROR "YCM_USE_CMAKE_NEXT set to OFF is not supported with CMake < 3.22")
  endif()
  if(YCM_USE_CMAKE_NEXT)
    set_property(GLOBAL PROPERTY YCM_USE_CMAKE_NEXT ON)
    list(APPEND CMAKE_MODULE_PATH ${_YCM_SRC_DIR}/cmake-next/proposed)
  else()
    set_property(GLOBAL PROPERTY YCM_USE_CMAKE_NEXT OFF)
  endif()

  # Use modules from specific CMake versions (default ON)
  foreach(_version 3.15
                   3.14
                   3.13)
    if(EXISTS "${YCM_MODULE_DIR}/cmake-${_version}"
      AND CMAKE_MINIMUM_REQUIRED_VERSION VERSION_LESS ${_version}
      AND CMAKE_VERSION VERSION_LESS ${_version})
      string(REPLACE "." "_" _VERSION ${_version})
      if(NOT DEFINED YCM_USE_CMAKE_${_VERSION} OR YCM_USE_CMAKE_${_VERSION})
        list(APPEND YCM_MODULE_PATH "${YCM_MODULE_DIR}/cmake-${_version}/Modules")
      endif()
    endif()
  endforeach()

endif()

# Use YCM 3rd party modules (default ON)
if(EXISTS "${YCM_MODULE_DIR}/3rdparty")
  if(NOT DEFINED YCM_USE_3RDPARTY OR YCM_USE_3RDPARTY)
    list(APPEND YCM_MODULE_PATH "${YCM_MODULE_DIR}/3rdparty")
  endif()
endif()

# Use YCM deprecated modules (default ON)
if(EXISTS "${YCM_MODULE_DIR}/deprecated")
  if(NOT DEFINED YCM_USE_DEPRECATED OR YCM_USE_DEPRECATED)
    list(APPEND YCM_MODULE_PATH "${YCM_MODULE_DIR}/deprecated")
    set(YCM_NO_DEPRECATED 0)
  endif()
endif()

list(APPEND CMAKE_MODULE_PATH "${YCM_MODULE_PATH}")


if(COMMAND set_package_properties)
  set_package_properties(YCM PROPERTIES DESCRIPTION "A collection of CMake modules and utilities for YARP and friends"
                                        URL "https://github.com/robotology/ycm")
endif()


if(NOT YCM_FIND_QUIETLY)
  message(STATUS "Found YCM: ${YCM_DIR} (found version \"${YCM_VERSION}\")")
endif()
